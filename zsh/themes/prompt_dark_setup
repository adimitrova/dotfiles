# vim: set filetype=zsh:
function prompt_dark_precmd {
    # Get Git repository information.
    if (( $+functions[git-info] )); then
        git-info
    fi
}

function prompt_dark_setup {
    setopt LOCAL_OPTIONS
    unsetopt XTRACE KSH_ARRAYS
    prompt_opts=(cr percent subst)

    # Make sure 256 colours are enabled
    export TERM=xterm-256color
    [ -n "$TMUX" ] && export TERM=screen-256color    # Workaround for tmux

    # Load required functions.
    autoload -Uz add-zsh-hook
    autoload -Uz vcs_info

    # Add a hook for calling info functions before each command.
    add-zsh-hook precmd prompt_dark_precmd

    # Set git-info parameters.
    zstyle ':prezto:module:git:info' verbose yes
    zstyle ':prezto:module:git:info:branch' format '%F{252}%b%f'
    zstyle ':prezto:module:git:info:dirty' format '%F{red}*%f'
    zstyle ':prezto:module:git:info:keys' format 'prompt' ' %b%D%C'

    local cmd_prefix='%B%(?.%F{blue}.%F{red})➤%f%b '
    PROMPT='%B%F{3}%3~%f%b %B${git_info[prompt]}%b'$'\n'${cmd_prefix}
}

prompt_dark_setup "$@"
